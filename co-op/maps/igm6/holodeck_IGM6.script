//-----------------------------------------------------------------
//
//  EF2 Level Script File
//
//  Level:	  HoloMission - 3
//  Script By:    K. Thompson
//  Geometry By:  K. Thompson
//  Created on:   01/10/2002
//
//-----------------------------------------------------------------

void startHoloTrainingMissionIGM6();

void photonBurstWaitIGM6();
void photonBurstGoIGM6();

//voided in ent-deck 8 IGM6 script
//void holoMissionIGM6Over();

////////////////////////////////
//  startHoloTrainingMissionIGM6
//  Start the holo mission
////////////////////////////////
void startHoloTrainingMissionIGM6()
{
	$triggerHoloDeck.thread( "" );

	killAllTalking();
	
	thread photonBurstWaitIGM6();
}

////////////////////////////////
// Wait till player picks up grenade launcher
////////////////////////////////
void photonBurstWaitIGM6()
{
	$photonBurst.ontouch( "photonBurstGoIGM6" );
	pause();
}

////////////////////////////////
// start mission
////////////////////////////////
void photonBurstGoIGM6()
{
	$photonBurst.notouch();
	$photonBurst.missionobjective( 0 );
	$photonBurst.remove();
	
	$holoDeckWeaponStand.notsolid();
	$holoDeckWeaponStand.hide();
	
	$player.give( "weapons/worldmodel-photon.tik" );
	$player.use( "photonburst" );

	thread givePlayerAmmo();
	
	wait( .2 );

	cam_fadeout( .5, 0, 0, 0, 1, 0 );
	spawn ( "trigger_changelevel", "targetname", "trigger_startTraining", "map", "ent-training_weap3" );
	dontSaveOrientation();
	wait ( .6 );
	trigger ( "$trigger_startTraining" );
}

////////////////////////////////
// end Mission
////////////////////////////////
void holoMissionIGM6Over()
{	
	// This is here so this function won't get called twice
	setfloatvar ("game.checkEntMission6RoomsVisited", 5.5 );

	waitforplayer();
	
	$player.immobilize( 0 );
		
	cam_fadein( .5, 0, 0, 0, 1, 0 );
	wait( .6 );
	
	unlockHoloDoor();
	$player.take( "photonburst" );
	killthread( "givePlayerAmmo" );
	$player.health( 100 );

	//$korban.playdialog("localization/sound/dialog/ent_m7/korban_effective.mp3", 1, 10000, 1 ); //Very effective. 
	//waitForDialog( $korban );
	$korban.playdialog("localization/sound/dialog/ent_m7/korban_almost.mp3", 1, 10000, 1 ); //Almost as good as a Klingon!
	waitForDialog( $korban );
	
	setfloatvar ("game.checkEntMission6RoomsVisited", 6 );
	
	setfloatvar( "game.IGM6MeetInBriefingRoom", 1 );
	justLoadedDeckThreadIGM6();
}








 













