//-----------------------------------------------------------------
//
//  EF2 Level Script File
//
//  Level:	      ent-deck11 - MISSION 4
//  Script By:    K.Thompson
//  Created on:   08/12/2002
//	Last Edited:  08/12/2002 - kt
//-----------------------------------------------------------------

void setupMission4Deck11();
void justLoadedDeckThreadIGM4();

void cinematicKleeyaIGM4();
void cinematicKleeyaIGM4Skipthread();
void cinematicKleeyaIGM4End();

void kleeya_workIGM4();
void kleeyaDialog4_1();

void ambientActorsIGM4();

void stratton_workIGM4();
void strattonDialog4_1();

void miyamoto_workIGM4();
void miyamotoDialog4_1();
void miyamotoDialog4_2();
void miyamotoDialog4_3();

void setTurboLiftVariableIGM4();

float tuvokHasCalledMunroToBridgeIGM4 = 0;

//===================================================================================================================
// Setup 
//===================================================================================================================

//---------------------
// setupMission4Deck11	
// does all the setup stuff for the mission
//---------------------
void setupMission4Deck11()
{
	//hzm coop mod Chrissstrahl, set the objective
	globalCoop_objectives_set("$$IGM4TalkWithKleeya$$","complete",1,0);
	
	//--- Fade Out
	cam_fadeout( .05, 0, 0, 0, 1, 0 );
	
	spawn( "Camera", "targetname", "cam1" );
	spawn( "Camera", "targetname", "cam2" );
	spawn( "Camera", "targetname", "cam3" );
	spawn( "Camera", "targetname", "cam4" );
	spawn( "Camera", "targetname", "cam5" );
	spawn( "Camera", "targetname", "cam6" );
	spawn( "Camera", "targetname", "cam7" );
	spawn( "Camera", "targetname", "cam8" );
	spawn( "Camera", "targetname", "cam9" );
	spawn( "Camera", "targetname", "cam10" );

	cam.load( "cam_igm4_deck11_1" ); //Inrto
	cam.load( "cam_igm4_deck11_2" ); 
	cam.load( "cam_igm4_deck11_3" ); //Key board
	cam.load( "cam_igm4_deck11_4" ); //Planet Shot 1
	cam.load( "cam_igm4_deck11_5" ); //Munro Mid
	cam.load( "cam_igm4_deck11_6" ); //Kleeya Mid
	cam.load( "cam_igm4_deck11_7" ); //Munro Tight
	cam.load( "cam_igm4_deck11_8" ); //Kleeya Tight
	cam.load( "cam_igm4_deck11_9" ); //Planet Shot 2

	//Close the hall portal
	astrometricsCloseHallPortal();

	globalCommon_SpawnActor( "char/kleeya-noweapon.tik", "kleeya", '-11072 1102 0', 90 );
	globalCommon_SpawnActor( "char/hazardteam_sf-munro.tik", "scriptmunro", '-11072 975 0', 90 );
	globalCommon_SpawnActor( "char/starfleet_tuvok.tik", "tuvok", '-11072 1152 0', 90 );
	
	spawn( "script_origin", "targetname", "lookAtOrigin" );
	
	$tuvok.hide();
	$tuvok.notsolid();
	
	thread cinematicKleeyaIGM4();
	
	thread ambientActorsIGM4();
}

//---------------------
// justLoadedDeckThreadIGM4
// Triggered and check to see if A cinematic should happen.
//------------------------
void justLoadedDeckThreadIGM4()
{
return;
	if( getfloatvar( "game.IGM4GoToTelsia" ) >= 1 )
	{
		$player.setobjectiveshow( "IGM4GoToTelsia", 1 );		
	}
	if( getfloatvar( "game.IGM4GoToTelsia" ) == 2 )
	{
		$player.setobjectivecomplete( "IGM4GoToTelsia", 1 );			
	}
	if( getfloatvar( "game.IGM4TalkWithKleeya" ) >= 1 )
	{
		$player.setobjectiveshow( "IGM4TalkWithKleeya", 1 );		
	}
	if( getfloatvar( "game.IGM4TalkWithKleeya" ) == 2 )
	{
		$player.setobjectivecomplete( "IGM4TalkWithKleeya", 1 );			
	}
		
	if( getfloatvar( "game.IGM4GoToBridge" ) >= 1 )
	{
		$player.setobjectiveshow( "IGM4GoToBridge", 1 );		
	}
	if( getfloatvar( "game.IGM4GoToBridge" ) == 2 )
	{
		$player.setobjectivecomplete( "IGM4GoToBridge", 1 );			
	}
	
	if( getfloatvar( "game.IGM4GoToArmory" ) >= 1 )
	{
		$player.setobjectiveshow( "IGM4GoToArmory", 1 );		
	}
	if( getfloatvar( "game.IGM4GoToArmory" ) == 2 )
	{
		$player.setobjectivecomplete( "IGM4GoToArmory", 1 );			
	}
	if( getfloatvar( "game.IGM4HoloDeck" ) >= 1 )
	{
		$player.setobjectiveshow( "IGM4HoloDeck", 1 );		
	}
	if( getfloatvar( "game.IGM4HoloDeck" ) == 2 )
	{
		$player.setobjectivecomplete( "IGM4HoloDeck", 1 );			
	}
}

//---------------------
// cinematic
// setup and begin cinematic
//---------------------
void cinematicKleeyaIGM4()
{
	freezeplayer();
	cinematic();
		
	// --- Setup
	//$player.hide();
	//$player.notsolid();
	
	// --- Que Cams
	$cam1.follow ( $cam_igm4_deck11_1 );
	$cam2.follow ( $cam_igm4_deck11_2 );
	$cam3.follow ( $cam_igm4_deck11_3 );
	//$cam4.follow ( $cam_igm4_deck11_4 );
	$cam5.follow ( $cam_igm4_deck11_5 );
	$cam6.follow ( $cam_igm4_deck11_6 );
	$cam7.follow ( $cam_igm4_deck11_7 );
	$cam8.follow ( $cam_igm4_deck11_8 );
	//$cam9.follow ( $cam_igm4_deck11_9 );
	
	$cam1.fov( 60 );
	$cam2.fov( 30 );
	$cam3.fov( 60 );
	$cam4.fov( 60 );
	$cam5.fov( 30 );
	$cam6.fov( 30 );
	$cam7.fov( 10 );
	$cam8.fov( 10 );
	$cam9.fov( 60 );
	$cam10.fov( 60 );

	$cam1.cut();
	$cam2.cut();
	$cam3.cut();
	$cam4.cut();
	$cam5.cut();
	$cam6.cut();
	$cam7.cut();
	$cam8.cut();
	$cam9.cut();
	$cam10.cut();
	
	cuecamera ( $cam1 );
	
	letterbox( .05 );
	
	wait( 1 );
	
	cuecamera ( $cam1 );
	
	//--- Start & Fade in
	cam_fadein( 1, 0, 0, 0, 1, 0 );
	
	// --- Skip Thread
	skipthread( "cinematicKleeyaIGM4Skipthread");
	
	$kleeya.walkto( "astrometricsNode0", "walk_fast" );
	wait( 3 );
	$scriptmunro.walkto( "astrometricsNode1" );
	
	wait( 6 );
	
	//--- Fade Out
	cam_fadeout( .5, 0, 0, 0, 1, 0 );
	wait( .6 );
	
	$scriptmunro.anim( "idle" );
	$kleeya.anim( "idle" );
	
	$kleeya.origin( '-11096 1800 10' );
	$kleeya.angle( 70 );
	
	$scriptmunro.origin( '-11048 1800 0' );
	$scriptmunro.angle( 140 );
	
	cuecamera ( $cam2 );
	
	$kleeya.headWatch( $scriptmunro, 10 );
	$scriptmunro.headWatch( $kleeya, 10 );
	
	wait( 1 );
	
	//--- Fade Out
	cam_fadein( .5, 0, 0, 0, 1, 0 );
	wait( .6 );
	
	$kleeya.playdialog("localization/sound/dialog/ent_m4/kleeya_mayi.mp3", 1, 10000, 0); //This ship is amazing. The resources here are overwhelming! If I had access this level of technology, I could have completed my work long ago. May I?
	waitForDialog( $kleeya );	
	
	cuecamera ( $cam3 );
	wait( .2 );	
	$scriptmunro.playdialog("localization/sound/dialog/ent_m4/munro_beguest.mp3", 1, 10000, 0); //Be my guest.
	wait( .75 );
	$kleeya.headWatch( $lookAtOrigin, 10 );
	wait( .25 );
	$scriptmunro.headWatch( $lookAtOrigin, 10 );
	//wait( .5 );
	
	//--- Fade Out
	cam_fadeout( .5, 0, 0, 0, 1, 0 );
	wait( .6 );
	
	fadeInKleeyaPlanets();
		
	wait( .75 );
	
	thread globalAccelMove_RotateY( $sunOrigin, 57, 18, "rampup", "" );
	
	$lookAtOrigin.origin( '-11160 1960 88' );
	$lookAtOrigin.time( 18 );
	$lookAtOrigin.moveEast( 128 );
	
	$cam4.follow ( $cam_igm4_deck11_4 );
	wait( .05 );
	cuecamera ( $cam4 );
	wait( .25 );
	
	//--- Fade Out
	cam_fadein( .5, 0, 0, 0, 1, 0 );
	wait( .6 );
	
	wait( .2 );
	$kleeya.playdialog("localization/sound/dialog/ent_m4/kleeya_idryll.mp3", 1, 10000, 0); //Here's my homeworld. Vaz'baj Seven to the Attrexians. I come from small island south of the largest continent. I haven't been back since I reached adolescence. I joined Inigor's research team while I was still in school.
	waitForDialog( $kleeya );
	$scriptmunro.headWatch( $kleeya, 10 );	
	wait ( .4 );
	
	cuecamera ( $cam5 );
	wait( .4 );
	$scriptmunro.playdialog("localization/sound/dialog/ent_m4/munro_whyleave.mp3", 1, 10000, 0); //Why did you leave so early?
	wait( .2 );
	$kleeya.headWatch( $scriptmunro, 10 );
	waitForDialog( $scriptmunro );

	cuecamera ( $cam6 );
	wait( .2 );
	$kleeya.playdialog("localization/sound/dialog/ent_m4/kleeya_truth.mp3", 1, 10000, 0); //There is no truth to be found in Idryll schools. They are controlled by the Attrexians. I had to leave to find real knowledge.
	waitForDialog( $kleeya );
	
	cuecamera ( $cam5 );
	$scriptmunro.playdialog("localization/sound/dialog/ent_m4/munro_didyoufind.mp3", 1, 10000, 0); //I left home early, too. But I couldn't wait to leave my planet. I had to get to the stars.
	waitForDialog( $scriptmunro );

	cuecamera ( $cam8 );
	$kleeya.playdialog("localization/sound/dialog/ent_m4/kleeya_peer.mp3", 1, 10000, 0); //Do you miss home?
	waitForDialog( $kleeya );
	wait( .2 );

	cuecamera ( $cam7 );
	wait( .2 );
	$scriptmunro.playdialog("localization/sound/dialog/ent_m4/munro_noidea.mp3", 1, 10000, 0); //Not until I thought I'd be stuck in the Delta Quadrant for the rest of my life. Then I couldn't wait to get home.
	waitForDialog( $scriptmunro );
	wait( .2 );
	
	cuecamera ( $cam8 );
	wait( .2 );
	$kleeya.playdialog("localization/sound/dialog/ent_m4/kleeya_brag.mp3", 1, 10000, 0); //Show me your world.
	waitForDialog( $kleeya );

	//--- Fade Out
	cam_fadeout( .5, 0, 0, 0, 1, 0 );
	wait( .6 );
	
	wait( 1 );
	
	fadeOutKleeyaPlanets();
	fadeInMunroPlanets();
	
	$sun.time( .05 );
	$sun.moveEast( 256 );

	$kleeya.headWatch( $lookAtOrigin, 10 );
	$scriptmunro.headWatch( $lookAtOrigin, 10 );

	wait( .75 );

	$cam9.follow ( $cam_igm4_deck11_9 );

	$moonOrigin.rotateY( -10 );
	//$moon.rotateY( 10 );
	
	$sunOrigin.time( .05 );
	$sunOrigin.rotateYup( -25 );
	wait( .05 );

	cuecamera ( $cam9 );
	wait( .25 );
	
	$lookAtOrigin.origin( '-10984 1960 88' );
	$lookAtOrigin.time( 8 );
	$lookAtOrigin.moveWest( 96 );
	
	$sunOrigin.time( 8 );
	$sunOrigin.rotateYup( -30 );
	//thread globalAccelMove_RotateY( $sunOrigin, -50, 8, "rampup", "" );
	
	//--- Fade Out
	cam_fadein( .5, 0, 0, 0, 1, 0 );
	wait( .6 );

	$scriptmunro.playdialog("localization/sound/dialog/ent_m4/munro_solthree.mp3", 1, 10000, 0); //This is Earth. Sol Three. I'm from an area called Vermont.
	waitForDialog( $scriptmunro );
	$kleeya.headWatch( $scriptmunro, 7 );
	wait( .5 );
	
	//developerMode = 0;
	
	if( developerMode == 1 )
	{
		cueplayer();
		noncinematic();
		releaseplayer();
		clearletterbox( .05 );
		
		//$kleeya.headWatch( $scriptmunro, 10 );
		//$scriptmunro.headWatch( $kleeya, 10 );	
		
		$player.warp( $scriptmunro.getOrigin() );
		pause();
	}
	
	cuecamera ( $cam6 );
	wait( .5 );
	$scriptmunro.headWatch( $kleeya, 5 );
	wait( .5 );
	$kleeya.playdialog("localization/sound/dialog/ent_m4/kleeya_itlike.mp3", 1, 10000, 0); //What’s it like?
	waitForDialog( $kleeya );
	
	cuecamera ( $cam5 );
	wait( .2 );
	$scriptmunro.playdialog("localization/sound/dialog/ent_m4/munro_resource.mp3", 1, 10000, 0); //It's a rugged place. Very independent and resourceful people.
	waitForDialog( $scriptmunro );
	
	cuecamera ( $cam8 );
	wait( .2 );
	$kleeya.playdialog("localization/sound/dialog/ent_m4/kleeya_likeyou.mp3", 1, 10000, 0); //Like you, Alexander.
	waitForDialog( $kleeya );
	
	cuecamera ( $cam7 );
	wait( .2 );
	$scriptmunro.playdialog("localization/sound/dialog/ent_m4/munro_well.mp3", 1, 10000, 0); //Call me Alex.
	waitForDialog( $scriptmunro );
	
	cuecamera ( $cam8 );
	wait( .2 );
	$kleeya.playdialog("localization/sound/dialog/ent_m4/kleeya_vermont.mp3", 1, 10000, 0); //I would love to see this land of Vermont, Alex.
	waitForDialog( $kleeya );
	
	cuecamera ( $cam3 );
	wait( .2 );
	$scriptmunro.resetHead();
	$tuvok.playdialog("localization/sound/dialog/ent_m4/tuvok_munrobrid.mp3", 1, 10000, 1); //Lieutenant Munro to the Bridge.
	waitForDialog( $tuvok );
	
	//hzm coop mod Chrissstrahl, set the objective
	globalCoop_objectives_set("$$IGM4TalkWithKleeya$$","complete",1,0);
	globalCoop_objectives_set("$$IGM4GoToBridge$$","incomplete",2,1);
	
	cuecamera ( $cam5 );
	wait( .2 );
	$scriptmunro.playdialog("localization/sound/dialog/ent_m4/munro_escort2.mp3", 1, 10000, 0); //On my way. Kleeya, I’ll have security escort you back to your quarters.
	wait( 1 );
	$scriptmunro.headWatch( $kleeya, 10 );
	waitForDialog( $scriptmunro );
	
	cuecamera ( $cam6 );
	wait( .2 );
	$kleeya.playdialog("localization/sound/dialog/ent_m4/kleeya_tooshort.mp3", 1, 10000, 0); //Very well.  Perhaps later you can tell me more about Earth.
	waitForDialog( $kleeya );

	//--- End Thread
	thread cinematicKleeyaIGM4End();
}

//---------------------
// cinematicSkipthread
// skipthread that calls the skipthread routines
//------------------------
void cinematicKleeyaIGM4Skipthread()
{
	//--- kill the cinematic thread
	skipthread( "Null" );
	killthread( "cinematicKleeyaIGM4" );
	thread cinematicKleeyaIGM4End();
}

//---------------------
// cinematicEnd
// wrapup the cinematic
//------------------------
void cinematicKleeyaIGM4End()
{
	//--- fade out and set the level up
	cam_fadeout( .5, 0, 0, 0, 1, 0 );
	wait( .75 );

	killAllTalking();
	
	thread kleeya_workIGM4();
	
	$kleeya.resetHead();
	$scriptmunro.resetHead();
	
	$scriptmunro.anim( "idle" );
	$kleeya.anim( "idle" );
	
	$kleeya.origin( '-11096 1800 2' );
	$kleeya.angle( 70 );
	
	$scriptmunro.hide();
	$scriptmunro.notsolid();
	
	$sunOrigin.time( .05 );
	$sunOrigin.rotateYup( 10 );
	wait( .05 );
	
	$sunOrigin.angles( '0 30 0' );
	$moonOrigin.rotateY( -10 );
	//$moon.rotateY( 10 );
	
	fadeOutKleeyaPlanets();
	fadeInMunroPlanets();
	//hzm coop mod chrissstrahl - i give a fuck for this part
	/*
	cueplayer();
	noncinematic();
	releaseplayer();
	*/
	//eof hzm
	
	clearletterbox( .05 );
	
	//hzm coop mod chrissstrahl - fuck this
	//cam_fadein( .5, 0, 0, 0, 1, 0 ) ;
		
	setfloatvar ("game.checkEntMission4RoomsVisited", 4 );
	
	//HZM COOP MOD Chrissstrahl - set mission status
	setFloatVar("game.igmRoomsVisited",4);//set rooms visited
	//eof HZM
	
	wait( .6 );
	
	//Turn Exit Triggers On
	$triggerExit.triggerable();
	
	//Objectives
	setfloatvar( "game.IGM4TalkWithKleeya", 2 );
	setfloatvar( "game.IGM4GoToBridge", 1 );
	justLoadedDeckThreadIGM4();
	
	//hzm coop mod chrissstrahl - allow player to go to brige
	setfloatvar(  "level.deckToChangeToIGM", 1 );
	
	//hzm coop mod chrissstrahl - load next map, just like that
	coop_igm_mapToLoad = "ent-deck1_bridge";
	thread coop_endLevel();
}

///////////////////////////
// kleeyaOnUseWaitIGM4
// Wait for player to talk to actor
///////////////////////////
void kleeya_workIGM4()
{
	setNextDialog( $kleeya, 1 );
	setDialogFunction( $kleeya, "kleeyaDialog" );
	while( 1 )
	{
		workUntilUsed( $kleeya, "", "", 0 );
		talkUntilDone( $kleeya );
	}
}

///////////////////////////
// kleeyaDialog4_1
// Player and actor talk
///////////////////////////
void kleeyaDialog4_1()
{	
	$kleeya.headwatch( $player, 10 );

	$kleeya.playdialog("localization/sound/dialog/ent_m4/kleeya_listening.mp3", 1, 128, 0); //I enjoyed talking to you.
	waitForDialog( $kleeya );
	
	setNextDialog( $kleeya, 1 );
	$kleeya.resethead();
	setDialogFinished( $kleeya );
}

//===================================================================================================================
// Ambient Actors
//===================================================================================================================
//---------------------
// ambientActorsIGM4	
// Set up ambient Actors
//---------------------
void ambientActorsIGM4()
{
	globalCommon_SpawnActor( "char/starfleet_crew-male3.tik", "stratton", '-9804 -836  4', 270 );
	//I do this so the door to astrometrics isn't open untill the cinematic characters walk through
	wait( 4 );
	$stratton.origin( '-11008 1376  0' );
	$stratton.pushable( 0 );
	thread stratton_workIGM4();
	
	thread randomlyAnimateActor( $stratton, "ent-guard" );
	
	globalCommon_SpawnActor( "char/starfleet_crew-female2.tik", "miyamoto", '-9780 -1108  8', 90 );	
	globalCommon_AiActorUseWeapon( $miyamoto, "EnterpriseDatapad" );
	$miyamoto.pushable( 0 );
	$miyamoto.anim( "ent-datapad-use" );

	thread miyamoto_workIGM4();
	
	globalCommon_SpawnActor( "char/starfleet_crew-male6.tik", "russel", '-10248 -272 0', 0 );
	$russel.setnodeid( 50 );
	$russel.alias( "russelIGMA1", "localization/sound/dialog/ent_m4/manaan_sorry2.mp3", 1, 128, 0); //Please don’t disturb me.
	$russel.dialog( "russelIGMA" );
	$russel.ai_on();
	//hzm coop mod chrissstrahl - don't allow them to be pushed or they might block entrance to the turbo
	$russel.pushable(0);

	globalCommon_SpawnActor( "char/hazardteam_sf-redshirt2.tik", "thompson", '-9960 740 0', 0 );
	$thompson.setnodeid( 50 );
	$thompson.alias( "thompsonIGMA1", "localization/sound/dialog/ent_m4/jacobs_notime2.mp3", 1, 128, 0); //Please don’t disturb me.
	$thompson.dialog( "thompsonIGMA" );
	$thompson.ai_on();
	//hzm coop mod chrissstrahl - don't allow them to be pushed or they might block entrance to the turbo
	$thompson.pushable(0);
}

///////////////////////////
// strattonOnUseWaitIGM4
// Wait for player to talk to actor
///////////////////////////
void stratton_workIGM4()
{
	setNextDialog( $stratton, 1 );
	setDialogFunction( $stratton, "strattonDialog" );
	while( 1 )
	{
		workUntilUsed( $stratton, "", "", 0 );
		talkUntilDone( $stratton );
	}
}

///////////////////////////
// strattonDialog4_1
// Player and actor talk
///////////////////////////
void strattonDialog4_1()
{
	$stratton.setFloatVar( "pauseRandomAnimate", 1 );
	wait( .25 );
	$stratton.anim( "ent-guard-scan" );
	$stratton.headwatch( $player, 10 );

	$stratton.playdialog("localization/sound/dialog/ent_m4/security_protect.mp3", 1, 128, 0 ); //Do you need protection?
	waitForDialog( $stratton );
	
	setNextDialog( $stratton, 1 );
	$stratton.resethead();
	$stratton.setFloatVar( "pauseRandomAnimate", 0 );
	setDialogFinished( $stratton );
}

///////////////////////////
// miyamotoOnUseWaitIGM4
// Wait for player to talk to actor
///////////////////////////
void miyamoto_workIGM4()
{
	setNextDialog( $miyamoto, 1 );
	setDialogFunction( $miyamoto, "miyamotoDialog" );
	while( 1 )
	{
		workUntilUsed( $miyamoto, "", "", 0 );
		talkUntilDone( $miyamoto );
	}
}

///////////////////////////
// miyamotoDialog4_1
// Player and actor talk
///////////////////////////
void miyamotoDialog4_1()
{
	$miyamoto.anim( "ent-datapad-scan" );
	$miyamoto.headwatch( $player, 10 );
	$miyamoto.turntowardsplayer();
	
	$miyamoto.playdialog("localization/sound/dialog/ent_m3/myamoto_helpyou.mp3", 1, 128, 0); //Can I help you?
	waitForDialog( $miyamoto );

	setNextDialog( $miyamoto, 2 );
	$miyamoto.resethead();
	$miyamoto.angle( 90 );
	$miyamoto.anim( "ent-datapad-use" );
	setDialogFinished( $miyamoto );
}

///////////////////////////
// miyamotoDialog4_2
// Player and actor talk
///////////////////////////
void miyamotoDialog4_2()
{
	$miyamoto.anim( "ent-datapad-scan" );
	$miyamoto.headwatch( $player, 10 );
	$miyamoto.turntowardsplayer();

	$miyamoto.playdialog("localization/sound/dialog/ent_m3/myamoto_whatit.mp3", 1, 128, 0); //What is it?
	waitForDialog( $miyamoto );

	setNextDialog( $miyamoto, 3 );
	$miyamoto.resethead();
	$miyamoto.angle( 90 );
	$miyamoto.anim( "ent-datapad-use" );
	setDialogFinished( $miyamoto );
}

///////////////////////////
// miyamotoDialog4_3
// Player and actor talk
///////////////////////////
void miyamotoDialog4_3()
{
	$miyamoto.anim( "ent-datapad-scan" );
	$miyamoto.headwatch( $player, 10 );
	$miyamoto.turntowardsplayer();

	$miyamoto.playdialog("localization/sound/dialog/ent_m3/myamoto_sorry.mp3", 1, 128, 0); //Sorry, I can't chat right now.
	waitForDialog( $miyamoto );

	setNextDialog( $miyamoto, 1 );
	$miyamoto.resethead();
	$miyamoto.angle( 90 );
	$miyamoto.anim( "ent-datapad-use" );
	setDialogFinished( $miyamoto );
}

//===================================================================================================================
// Deck Change Check
//===================================================================================================================
//---------------------
// checkForDeckChangeIGM4
//---------------------
void checkForDeckChangeIGM4()
{
	//hzm coop mod chrissstrahl - this is done in the global turbo igm script
	return;
	if( getfloatvar( "game.checkEntMission4RoomsVisited" ) == 4 )
	{
		setfloatvar(  "level.deckToChangeToIGM", 1 );

		$scriptmunro.playdialog("localization/sound/dialog/ent_m1/munro_d1bridge.mp3", 1, 10000, 0); //Deck 1: Bridge
	
		thread setTurboLiftVariableIGM4();
	}
}

//---------------------
// makse it so turbo lift doesn't move sound in and open its doors.
//---------------------
void setTurboLiftVariableIGM4()
{
	//Wait here so the variable has time to be set normaily
	//before I cange it for this special case
	wait( 2 );
	setfloatvar ( "game.globalTurboliftRide" , 0 );
}





































