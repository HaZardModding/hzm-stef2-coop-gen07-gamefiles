//-----------------------------------------------------------------
//
//  EF2 Level Script File
//
//  Level:	      ent-deck7b - MISSION 4
//  Script By:    K.Thompson
//  Created on:   08/12/2002
//	Last Edited:  08/12/2002 - kt
//-----------------------------------------------------------------

void setupMission4Deck7b();

void cinematicDrullShuttleLeavesIGM4();
void cinematicDrullShuttleLeavesIGM4Skipthread();
void cinematicDrullShuttleLeavesIGM4Scene2();
void cinematicDrullShuttleLeavesIGM4End();

//-----------------------------
//--- variables

entity cinematicArm_IGM4_Shuttlebay;


//===================================================================================================================
// Setup
//===================================================================================================================

//---------------------
// setupMission4Deck7b
// does all the setup stuff for the mission
//---------------------
void setupMission4Deck7b()
{
	//--- Fade Out
	cam_fadeout( .05, 0, 0, 0, 1, 0 );

	spawn( "Camera", "targetname", "cam1" );

	cam.load( "cam_igm4_deck7b_1" );

	$tempshuttle.remove();
	//$shuttleExterior.anim( "closed_new" );
	//globalShuttle_Setup( "sydney", "chang", "chell", "jurot" );
	$shuttleExterior.hide();


	// setup DrullShuttle
	initDrullShuttle( "shuttle" );

	$drullShuttleCenterOrigin.origin( $shuttleStartOrigin0.getorigin() );

	$drullShuttleCenterOrigin.time( .05 );
	$drullShuttleCenterOrigin.moveUp( 200 );
	waitfor( $drullShuttleCenterOrigin );

	$drullShuttleCenterOrigin.bind( $shuttleStartOrigin2 );

	// Spawn Actors
	globalCommon_SpawnActor( "char/starfleet_tuvok.tik", "tuvok", '-8644 -5068 -216', 45 );

	globalCommon_SpawnActor( "char/inigor-noweapon.tik", "inigor", '-8556 -4972 -216', 225 );

	globalCommon_SpawnActor( "char/drull_son-male.tik", "krindo", '-8524 -5052 -216', 185 );

	globalCommon_SpawnActor( "char/kleeya-noweapon.tik", "kleeya", '-8692 -5044 -216', 25 );

	globalCommon_SpawnActor( "char/hazardteam_sf-munro.tik", "scriptmunro", '-8612 -5100 -216', 45 );

	//Actors For Dialog
	globalCommon_SpawnActor( "char/starfleet_picard.tik", "picard", '-8644 -5068 -216', 45 );
	$picard.hide();
	$picard.notsolid();

	//--- armature setup
	cinematicArm_IGM4_Shuttlebay = createCinematic( "igm4_leave" );
	cinematicArm_IGM4_Shuttlebay.setendthread( "cinematicDrullShuttleLeavesIGM4Scene2" );

	developerMode = 0;

	if( developerMode == 1 )
	{
		cinematicDrullShuttleLeavesIGM4Scene2();
	}

	thread cinematicDrullShuttleLeavesIGM4();
}

//---------------------
// cinematicDrullShuttleLeavesIGM4
// setup and begin cinematicDrullShuttleLeavesIGM4
//---------------------
void cinematicDrullShuttleLeavesIGM4()
{
	//--- starting cinematic commands
	freezeplayer();
	cinematic();

	//--- fade out
	cam_fadeout( .5, 0, 0, 0, 1, 0 );
	wait( .6 );

	letterbox( .1 );
	
	//hzm coop mod chrissstrahl - Wait until the Game Starts in Multiplayer
	globalCoop_main_waitForWarmupTime();

	//--- hide the player and AI
	$player.notsolid();
	$player.hide();
	globalCommon_AiDummyHide( $scriptmunro );

	//allowmusicducking ( 0 );
	music ( "aux2" );
	//--- start the armature
	cinematicArm_IGM4_Shuttlebay.beginCinematic( "igm4_leave" );
	wait( .2 );

	//--- fade in
	cam_fadein( 1, 0, 0, 0, 1, 0 );

	//--- set the skipthread
	skipthread( "cinematicDrullShuttleLeavesIGM4End" );
}

//---------------------
// cinematicDrullShuttleLeavesIGM4Scene2
//------------------------
void cinematicDrullShuttleLeavesIGM4Scene2()
{
	//--- fade out and set the level up
	cam_fadeout( .5, 0, 0, 0, 1, 0 );
	wait( .75 );

	$shuttlePath01b0.speed( 1 );
	$shuttlePath01b1.speed( 1 );
	$shuttlePath01b2.speed( .5 );
	$shuttlePath01b3.speed( .5 );
	$shuttlePath01b4.speed( 1 );
	$shuttlePath01b5.speed( 1 );
	$shuttlePath01b6.speed( 1 );
	$shuttlePath01b7.speed( 1 );
	$shuttlePath01b8.speed( 1 );

	// --- Que Cams
	$cam1.follow ( $cam_igm4_deck7b_1 );

	$cam1.fov( 60 );

	$cam1.cut();

	$sky.rendereffects( "+skyorigin" );
	$enterpriseE1.show();
	$enterpriseE2.show();
	$enterpriseE3.show();

	thread openLeftShuttleBayDoor();

	cuecamera( $cam1 );

	wait( .05 );

	//--- Start & Fade in
	cam_fadein( .5, 0, 0, 0, 1, 0 );
	wait( .5 );

	wait( 2.5 );

	$shuttleStartOrigin2.playsound( "sound/vehicles/shuttlecraft/shuttle_takeoff.wav", 3, 1, 3000 );

	$shuttleStartOrigin2.followpath( $shuttlePath01b0 );
	wait( 6 );

	//--- fade out and set the level up
	cam_fadeout( 2, 0, 0, 0, 1, 0 );
	wait( 2.05 );

	//--- End Thread
	thread cinematicDrullShuttleLeavesIGM4End();
}

//---------------------
// cinematicDrullShuttleLeavesIGM4Skipthread
// skipthread that calls the skipthread routines
//------------------------
void cinematicDrullShuttleLeavesIGM4Skipthread()
{
	//--- kill the cinematicDrullShuttleLeavesIGM4 thread
	skipthread( "Null" );
	killthread( "cinematicDrullShuttleLeavesIGM4Scene2" );

	thread cinematicDrullShuttleLeavesIGM4End();
}


//---------------------
// cinematicDrullShuttleLeavesIGM4End
// wrapup the cinematicDrullShuttleLeavesIGM4
//------------------------
void cinematicDrullShuttleLeavesIGM4End()
{
	//kill the cinematic
	skipthread( "null" );

	//fade to black
	cam_fadeout( .5, 0, 0, 0, 1, 0 );
	wait( .6 );

	//--- end the armature cinematics, FALSE means don't launch the end thread
	cinematicArm_IGM4_Shuttlebay.endCinematic( FALSE );

	killthread( "cinematicDrullShuttleLeavesIGM4" );
	killthread( "cinematicDrullShuttleLeavesIGM4Scene2" );

	//--- IGM stuff
	setfloatvar ("game.checkEntMission4RoomsVisited", 6 );
	
	//HZM COOP MOD Chrissstrahl - set mission status
	setFloatVar("game.igmRoomsVisited",6);//set rooms visited
	//eof HZM

	//--- set the player
	$player.solid();
	$player.show();

	if( developerMode == 1 )
	{
		//--- release the cinematic
		noncinematic();
		clearletterbox( .1 );
		releaseplayer();
		cueplayer();

		//--- Start & Fade in
		cam_fadein( .5, 0, 0, 0, 1, 0 );
		wait( .75 );

		pause();
	}

	//--- change levels
	spawn ( "trigger_changelevel", "targetname", "trigger_endlevel", "map", "ent-deck1_bridge" );
	wait ( .1 );
	trigger ( "$trigger_endlevel" );
}

//===================================================================================================================
// Deck Change Check
//===================================================================================================================
//---------------------
// checkForDeckChangeIGM4
//---------------------
void checkForDeckChangeIGM4()
{
	if( getfloatvar( "game.checkEntMission4RoomsVisited" ) == 4 )
	{
		setfloatvar(  "level.deckToChangeToIGM", 1 );

		$scriptmunro.playdialog("localization/sound/dialog/ent_m1/munro_d1bridge.mp3", 1, 10000, 0); //Deck 1: Bridge
	}
	if( getfloatvar( "game.checkEntMission4RoomsVisited" ) == 6 )
	{
		setfloatvar(  "level.deckToChangeToIGM", 11 );

		$scriptmunro.playdialog("localization/sound/dialog/ent_m1/munro_d11astro.mp3", 1, 10000, 0); //Deck 11: Astrometrics
	}
}























