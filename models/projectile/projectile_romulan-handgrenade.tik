TIKI
setup
	{
   scale 0.40
   path models/weapons/grenade_launcher/viewmodel
   surface material1 shader weapons/grenadeskin2
   surface material2 shader weapons/grenadeskin1
	}

//====================================================
//Includes and Defines
//====================================================
$include models/actorweapons/actorprojectiledata.tik

init
	{
	server
		{
		classname				Projectile
		notifyactors				1
		setsize					"-8 -8 -8" "8 8 8"
		health					10
		takedamage
		speed					$ROM-HANDGRENADE-PROJ-SPEED$
		hitdamage				$ROM-HANDGRENADE-DIRECT-DAMAGE$
		meansofdeath				$ROM-HANDGRENADE-MOD$
		explosionmodel				$ROM-HANDGRENADE-PROJ-EXPLOSION$
		cache					models/$ROM-HANDGRENADE-PROJ-EXPLOSION$
		gravity					$ROM-HANDGRENADE-PROJ-GRAVITY$
		knockback				$ROM-HANDGRENADE-PROJ-KNOCKBACK$
		life					$ROM-HANDGRENADE-PROJ-LIFE$
		bouncefactor            		0.5
		bouncetouch
		minOnGroundTime				0.15
		bouncesound				sound/weapons/grenade/gren_bounce.wav
		cache 					sound/weapons/grenade/gren_bounce.wav

		}
	client
		{
		cache sound/ships/romulan/rom_grenloop.wav
		cache models/fx/fx-trail.tik
		cache compressionspike3.spr

      		originemitter lightflare
	         	(
	         	dlight 0.05 1.0 0.275 96 0
	         	)

		originemitter flare
			(
			hardlink
			spawnrate 9//10
			model compressionspike3.spr
			color 0.05 0.9 0.25
			angles 0 0 crandom 180
			avelocity 0 0 crandom 160
			life 0.4
			scale 0.275
			scalerate -0.5
			dofade 0 0 0
			)

		}
	}

animations
	{
	idle 	grenade1.tan
		{
		client
			{
		0 loopsound sound/ships/romulan/rom_grenloop.wav 2 100

		entry originspawn
			(
			hardlink
			count 2
			model compressionspike3.spr
			color 0.05 0.9 0.25
			angles 0 0 crandom 180
			avelocity 0 0 crandom 160
			life 1.0
			scale 0.275
			scalerate -1.0
			dofade 0 0 0
			)

	      	entry originspawn
			(
			hardlink
			model models/fx/fx-trail.tik
			life 3.0
			scale 1.2
			trail glowbeam-green-01 tag_left tag_right 0.65
			)

			}
		}

   onground 	grenade1.tan
	{
	server
		{
		entry explode
		}
	}

	}
