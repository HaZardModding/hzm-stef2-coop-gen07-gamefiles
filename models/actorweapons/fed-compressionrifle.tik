TIKI
setup
{
	scale 1.0
	path models/weapons/comp_rifle/worldmodel
	skelmodel comp-rifle-base.skb
	texturepath models/weapons/comp_rifle/viewmodel

	surface material10 shader weapons/comprifleenv-worldmodel
	surface material11 shader weapons/compglow
	surface material12 shader weapons/compressionscreen-world

	loadallanims
}

//====================================================
//Includes and Defines
//====================================================
$include models/actorweapons/actorweapondata.tik
$define compdir sound/weapons/comprifle

init
{
	server
		{
		//======================================
		// General Setup
		//======================================
		classname							Weapon
		name								"CompressionRifle"
		archetype							CompRifle
		hand								dual
		maxrange							3000

		//======================================
		// Mode Data
		//======================================
		modeset MODE1 firetype				$COMPRIFLE-FIRE-TYPE$
		modeset MODE1 bulletrange			$COMPRIFLE-BULLET-RANGE$
		modeset MODE1 bulletspread			$COMPRIFLE-BULLET-SPREAD$ $COMPRIFLE-BULLET-SPREAD$
		modeset MODE1 bulletdamage			$COMPRIFLE-BULLET-DAMAGE$
		modeset MODE1 meansofdeath			$COMPRIFLE-MOD$
		modeset MODE1 loopfire
		}

	client
		{
			cache models/fx/fx-compressionring.tik
		}
}

animations
{
	//======================================
	// Fire Animation
	//======================================
	fire1								fire1.ska crossblend 0
	{
	server
		{
		0 shoot
		entry finishedfiring 0
		last finishedfiring 1
		exit finishedfiring 1
		}
	client
		{
		0 sound $compdir$/compfire1.wav 2 1 300 .9 1.2
		0 tagdlight tag_barrel 0.8 0.7 0.7 384 0.075

		0 tagbeamspawn tag_barrel
			(
			beamlength $COMPRIFLE-BULLET-RANGE$
			spread $COMPRIFLE-BULLET-SPREAD$ $COMPRIFLE-BULLET-SPREAD$
			canendearly
			alwayscreate
			beamshader compressionbeam01.spr
			scale 3.75
			life 0.1
			fade
			)

		0 tagspawn tag_barrel
			(
			hardlink
			model compressionflare.spr
			scale 0.07
			angles 0 0 crandom 180
			offsetalongaxis 0 0 -1
			scalerate 1.5
			life 0.1
			)

		0 tagspawn tag_barrel
			(
			hardlink
			model flarespark.spr
			color 0.675 0.435 0.275
			count 7
			scale 0.13
			angles 0 0 crandom 180
			scalerate 3.3
			life 0.1
			)

		0 tagspawn tag_barrel
			(
			hardlink
			model compressionwave.spr
			scale 0.04
			velocity 330
			angles 0 0 crandom 360
			offsetalongaxis 0 0 2
			alpha 0.35
			scalerate 13.0
			life 0.21
			dofade 0.1 0 0
			)

		0 tagtraceimpactspawn tag_barrel
			(
			uselasttraceend
			model orangesparkle.spr
			count 10
			randvelaxis randomrange 30 170 crandom 100 randomrange -20 -280
			collision
			bouncefactor 0.365
			life randomrange 1.2 1.8
			scale 0.075
			accel 0 0 -685
			scalerate -0.75
			hitentities 1
			dofade 1.15 0 0
			)

		0 tagtraceimpactspawn tag_barrel
			(
			uselasttraceend
			model compressionimpact.spr
			align
			offsetalongaxis 0 0 0
			life 3.75
			scale 0.35
			scalerate -0.75
			dofade 3.0 0 0
			)

		0 tagtraceimpactspawn tag_barrel
			(
			uselasttraceend
			count 7
			model models/fx/fx-compressionring.tik
			angles random 180 random 180 random 180
			alpha 0.55
			life 0.4
			avelocity crandom 250 crandom 250 crandom 250
			scale 1.0
			scaleupdown
			hitentities 1
			fade
			)

		0 tagtraceimpactspawn tag_barrel
			(
			uselasttraceend
			count 10
			model fireexplosion-anim.spr
			color 0.6 0.35 0.0
			angles 0 0 crandom 180
			alpha 0.45
			life 0.4
			avelocity 0 0 crandom 150
			offsetalongaxis crandom 6 crandom 6 crandom 6
			randvelaxis crandom 4 crandom 4 crandom 4
			scale 0.18
			scalerate 0.35
			hitentities 1
			fade
			)

		0 tagtraceimpactspawn tag_barrel
			(
			uselasttraceend
			count 5
			model rom-bfg-saturation2.spr
			color 1 0.5 0.1
			angles random 180 random 180 random 180
			life 0.65
			avelocity crandom 225 crandom 225 crandom 225
			scale 0.5
			scaleupdown
			hitentities 1
			fade
			)
		}
	}


	//======================================
	// Idle Common 1 Animation
	//======================================
	idle	idle1.ska
}
